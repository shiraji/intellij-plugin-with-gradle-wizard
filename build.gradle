buildscript {
    ext.kotlin_version = '1.0.2-1'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.0.43"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'kotlin'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

ext {
    versionFile = project.file("VERSION")
    versionName = versionFile.readLines()[0].trim()
}

intellij {
    version '2016.1.3'
    plugins = ['Kotlin', 'devkit', 'gradle']
    pluginName 'gradle-intellij-plugin wizard'

    updateSinceUntilBuild = false
    sameSinceUntilBuild = false
    publish {
        username 'shiraji'
        password System.getenv('PASSWORD')
        pluginId '8450'
    }
}

group 'com.github.shiraji.ipgwizard'
version versionName

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

task prepareForNextDevelopment << {
    def versionParts = versionName.split(/\./)
    versionParts[2] = (versionParts[2] as int) + 1
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}-SNAPSHOT"
    versionFile.write(newVersion + "\n")
}

task prepareForRelease << {
    def versionParts = versionName.split(/\./)
    versionParts[2] = versionParts[2].split(/-/)[0]
    def newVersion = "${versionParts[0]}.${versionParts[1]}.${versionParts[2]}"
    versionFile.write(newVersion + "\n")
    new File("$projectDir/.travis/release").createNewFile()
}